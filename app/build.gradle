apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../personal_signing.gradle'
} catch (ignored) {
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        applicationId "io.github.zwieback.familyfinance"
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode 10505
        versionName "1.5.5"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    buildTypes {
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ignored) {
            }
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // support libs
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'

    // JPA
    implementation "io.requery:requery:${versions.requeryLib}"
    implementation "io.requery:requery-android:${versions.requeryLib}"
    implementation "io.requery:sqlite-android:${versions.sqliteAndroidLib}"
    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:${versions.rxJavaLib}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroidLib}"
    // icons
    implementation "com.mikepenz:iconics-core:${versions.iconicsLib}"
    implementation "com.mikepenz:iconics-views:${versions.iconicsLib}"
    implementation "com.mikepenz:material-design-iconic-typeface:${versions.materialDesignTypeface}"
    implementation "com.mikepenz:fontawesome-typeface:${versions.fontawesomeTypeface}"
    implementation "com.mikepenz:community-material-typeface:${versions.communityMaterialTypeface}"
    // materialize
    implementation "com.mikepenz:materialize:${versions.materializeLib}"
    // fast adapter
    implementation "com.mikepenz:fastadapter:${versions.fastAdapterLib}"
    implementation "com.mikepenz:fastadapter-commons:${versions.fastAdapterLib}"
    // material drawer
    implementation("com.mikepenz:materialdrawer:${versions.materialDrawerLib}") {
        transitive = true
        exclude module: "fastadapter"
        exclude module: "iconics-core"
        exclude group: "com.android.support"
    }
    // java.time
    implementation "com.jakewharton.threetenabp:threetenabp:${versions.timeLib}"
    // java 8 streams
    implementation "com.annimon:stream:${versions.streamLib}"
    // crash report
    releaseImplementation "ch.acra:acra-mail:${versions.acraLib}"
    releaseImplementation "ch.acra:acra-dialog:${versions.acraLib}"
    // preferences
    implementation "com.takisoft.fix:preference-v7:${versions.preferenceCompatLib}"
    // shared preferences
    implementation "ds.gendalf:gendalf:${versions.gendalfLib}"
    // file/directory picker
    implementation "com.nononsenseapps:filepicker:${versions.filepickerLib}"
    // permissions dispatcher
    implementation("com.github.hotchemi:permissionsdispatcher:${versions.permissionsDispatcherLib}") {
        exclude module: "support-v13"
    }
    // RxBinding
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:${versions.rxBinding2Lib}"
    // chart
    implementation "com.github.PhilJay:MPAndroidChart:${versions.mpAndroidChartLib}"
    // project libs
    implementation project(':shared')
    implementation project(':validator')
    testImplementation 'junit:junit:4.12'
    annotationProcessor "io.requery:requery-processor:${versions.requeryLib}"
    annotationProcessor "ds.gendalf:compiler:${versions.gendalfLib}"
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:${versions.permissionsDispatcherLib}"

    // UiAutomator Testing
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    implementation 'androidx.annotation:annotation:1.0.0'
}
